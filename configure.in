dnl Process this file with autoconf to produce a configure script.
AC_INIT(gvs, v4.21)
AC_CONFIG_SRCDIR(src/2A-52i/tsdis_toolkit/TS_2A52_60.h)

AM_INIT_AUTOMAKE
AM_CONFIG_HEADER(config.h)

dnl Default for GVS and friends.
AC_PREFIX_DEFAULT(/usr/local/trmm/GVBOX)

dnl Checks for programs.
AC_PROG_CC
AM_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_F77
AC_PROG_CPP
AC_PROG_CXX
AC_PATH_PROGS(perlbin,perl)

dnl Checks for libraries.
if test $prefix = NONE; then
  prefix=$ac_default_prefix
fi
LIBDIR="-L$prefix/lib"
LIBS="-lz -lm"

#AC_MSG_RESULT([Value of LIBDIR  $LIBDIR])

# The order of the libraries is important.
# This works:
#   -lgvs -lgv_utils -lrsl -limplode -letor -ltsdistk -lmfhdf -ldf -ljpeg -lz -lm
#
# If mounting with shared libraries we may have to specify all possible libraries.
# First check with simple LIBS list, if it fails, then expand the list.

AC_CHECK_LIB(implode,  _implode,           ,,$LIBDIR)
AC_CHECK_LIB(jpeg,     jpeg_CreateCompress,,,$LIBDIR)
AC_CHECK_LIB(df,       DFopen,             ,,$LIBDIR)
AC_CHECK_LIB(mfhdf,    SDstart,            ,,$LIBDIR)
AC_CHECK_LIB(tsdistk,  TKopen,             ,,$LIBDIR)

# Check for RSL now, the other libs are order independant; check a variety of aux libs.
# Disable cache.
L=""
unset ac_cv_lib_rsl_RSL_print_version
AC_CHECK_LIB(rsl,      RSL_print_version,
             L="-lrsl",
             unset ac_cv_lib_rsl_RSL_print_version,
             $LIBDIR)
if test "$L" = ""; then
AC_CHECK_LIB(rsl,      RSL_print_version,
             L="-lrsl -letor",
             unset ac_cv_lib_rsl_RSL_print_version,
             $LIBDIR -letor)
fi
LIBS="$L $LIBS"
L=""

AC_CHECK_LIB(etor,      RSL_to_EDGE, , , $LIBDIR)

AC_CHECK_LIB(gv_utils,      gv_utils_adjust_date, , , $LIBDIR)

LIBS="$LIBDIR $LIBS"

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h malloc.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_TM

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(mktime strdup strstr)

AC_CONFIG_FILES([src/gvslib/Makefile

 script/Makefile
 
 doc/Makefile 

 src/Makefile

 src/level_1/Makefile
 src/level_1/data/Makefile

 src/2A-52i/Makefile
 src/2A-52i/tsdis_toolkit/Makefile

 src/2A-53/Makefile
 src/2A-53/ver4/Makefile
 src/2A-53/ver4/tsdis_toolkit/Makefile
 src/2A-53/danny/Makefile
 src/2A-53/witek/Makefile
 src/2A-53/witek/data/Makefile
 src/2A-53/pi-c/Makefile
 src/2A-53/dual-cappi/Makefile
 src/2A-53/dual-cappi/tsdis_toolkit/Makefile

 src/2A-54/Makefile
 src/2A-54/src/Makefile
 src/2A-54/src/tsdis_toolkit/Makefile

 src/2A-55/Makefile
 src/2A-55/src/Makefile
 src/2A-55/src/tsdis_toolkit/Makefile
 src/2A-55/src/geomap/Makefile
 src/2A-55/src/geomap/maps/Makefile

 src/2A-56/Makefile

 src/2A-57/Makefile

 src/3A-53/Makefile
 src/3A-54/Makefile
 src/3A-55/Makefile
 src/3A-55/tsdis_toolkit/Makefile

 src/utils/Makefile

 format_conversion/Makefile
 format_conversion/tsdis_toolkit/Makefile

 src/acc_rrmaps/Makefile
 src/acc_rrmaps/tsdis_toolkit/Makefile

 Makefile
])
AC_OUTPUT
